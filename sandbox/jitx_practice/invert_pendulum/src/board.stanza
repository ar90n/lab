; Generated by JITX 3.34.0
#use-added-syntax(jitx)
defpackage invert-pendulum/board :
  import core
  import jitx
  import jitx/commands
  import jitx/parts

  import helpers
  import jsl

  import invert-pendulum/power
  import invert-pendulum/connectors
  import invert-pendulum/imu
  import invert-pendulum/motor
  import invert-pendulum/encoder

val BASE_BOARD_W = 78.0
val BASE_BOARD_H = 128.0
public val base-board-shape = RoundedRectangle(BASE_BOARD_W, BASE_BOARD_H, 0.25)

public pcb-module top-level :
  inst CONNECTORS : connectors
  net V_BAT (CONNECTORS.BAT.V+)
  net GND (CONNECTORS.BAT.V-)

  inst PWR : power-system 
  net (PWR.BAT CONNECTORS.BAT)
  net (PWR.V5 CONNECTORS.V5)

  inst IMU : imu
  net (IMU.V3V3 PWR.V3V3)
  net (IMU.I2C CONNECTORS.WIO_I2C0)

  inst MOTOR : motor-system
  net (MOTOR.PWR PWR.BUS)
  net (MOTOR.AIN CONNECTORS.WIO_M1CTRL)
  net (MOTOR.BIN CONNECTORS.WIO_M2CTRL)
  net (MOTOR.nSLEEP CONNECTORS.WIO_nSLEEP)
  net (MOTOR.AOUT CONNECTORS.M1)
  net (MOTOR.BOUT[0] CONNECTORS.M2[1])
  net (MOTOR.BOUT[1] CONNECTORS.M2[0])

  symbol(GND) = GND-SYMB
  symbol(V_BAT) = PWR-SYMB

  geom(GND) :
    copper-pour(LayerIndex(0), isolate = 0.125, rank = 1) = base-board-shape
    copper-pour(LayerIndex(1), isolate = 0.125, rank = 1) = base-board-shape

  val WIO_W = 72.0
  val WIO_H = 57.0
  val R     = 3.0
  val WIO_OFF_Y = BASE_BOARD_H / 2.0 - 15.0 - 2.0
  place(CONNECTORS.CON_WIO) at loc(0.0, WIO_OFF_Y) on Top
  layer(Courtyard(Top)) = 
    loc(0.0, WIO_OFF_Y - 13.5) * RoundedRectangle(WIO_W, WIO_H, R)

  val TT_W = 24.0
  val TT_H = 65.0
  val TT_OFF_Y = BASE_BOARD_H / -2.0 + (TT_H / 2.0 + 2.0)
  val LEFT_TT_OFF_X = BASE_BOARD_W / 2.0 - (TT_W / 2.0 + 2.0)
  val RIGHT_TT_OFF_X = BASE_BOARD_W / -2.0 + (TT_W / 2.0 + 2.0)
  val TT_PATTERN = RoundedRectangle(TT_W, TT_H, R)
  layer(Courtyard(Top)) = 
    loc(LEFT_TT_OFF_X, TT_OFF_Y) * TT_PATTERN
  layer(Silkscreen("body", Top)) = Text("Left Motor", 2.0, C, loc(LEFT_TT_OFF_X, TT_OFF_Y))
  layer(Courtyard(Top)) = 
    loc(RIGHT_TT_OFF_X, TT_OFF_Y) * TT_PATTERN
  layer(Silkscreen("body", Top)) = Text("Right Motor", 2.0, C, loc(RIGHT_TT_OFF_X, TT_OFF_Y))

val ENC_BOARD_W = 15.0
val ENC_BOARD_H = 22.3
public val enc-board-shape = RoundedRectangle(ENC_BOARD_W, ENC_BOARD_H, 0.25)

public pcb-module enc :
  name = "Encoder Board"
  inst ENCODER: encoder-system
  inst CON : create-part(mpn = "KH-2.54FH90-1X4P-H8.5", manufacturer = "Shenzhen Kinghelm Elec")

  place(ENCODER.U_ENC) at loc(0.0, 0.9, 0.0) on Top

  net V_ENC (ENCODER.PWR.V+)
  net GND (ENCODER.PWR.V-)

  net (CON.p[1] V_ENC)
  net (CON.p[2] GND)
  net (CON.p[3] ENCODER.I2C.sda)
  net (CON.p[4] ENCODER.I2C.scl)

  symbol(GND) = GND-SYMB
  symbol(V_ENC) = PWR-SYMB

  geom(GND) :
    copper-pour(LayerIndex(0), isolate = 0.125, rank = 1) = base-board-shape
    copper-pour(LayerIndex(1), isolate = 0.125, rank = 1) = base-board-shape
